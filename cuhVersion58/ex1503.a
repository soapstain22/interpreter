; ex1503.a  Structs in C++
startup:  bl main
          halt
;==============================================================
                           ; #include <iostream>
                           ; using namespace std;
                           ; struct A
                           ; {
                           ;    int x;
                           ;    int y;
                           ; };

@set$p1Aii:                ; void set(A* r, int n, int m)
          push lr          ; {
          push fp
          mov fp, sp
                           
          ldr r0, fp, 3    ;    r->x = n;
          ldr r1, fp  2
          str r0, r1, 0
                           
          ldr r0, fp, 4    ;    r->y = m;
          ldr r1, fp  2
          str r0, r1, 1
                           
          mov sp, fp       ; }
          pop fp
          pop lr
          ret

@display$p1A:              ; void display(A* r)
          push lr          ; {
          push fp
          mov fp, sp
                           
          ldr r0, fp, 2    ;    cout << r->x << endl;
          ldr r0, r0, 0
          dout r0
          nl
                           
          ldr r0, fp, 2    ;    cout << r->y << endl;
          ldr r0, r0, 1
          dout r0
          nl
                           
          mov sp, fp       ; }
          pop fp
          pop lr
          ret
;==============================================================
main:     push lr           ; int main()
          push fp           ; {         
          mov fp, sp
                            
          sub sp, sp, 1     ;    A *p, *q;
          sub sp, sp, 1
                            
          mov r1, 2         ;    p = (A *)malloc(sizeof(A));
          bl malloc
          str r0, fp, -1

          mov r1, 2         ;    q = (A *)malloc(sizeof(A));
          bl malloc
          str r0, fp, -2

          mov r0, 6         ;    p->set(5, 6);
          push r0
          mov r0, 5
          push r0
          ldr r0, fp, -1
          push r0
          bl @set$p1Aii
          add sp, sp, 3
                            
          ldr r0, fp, -1    ;    p->display();
          push r0
          bl @display$p1A
          add sp, sp, 1
                            
          mov r0, 11        ;    q->set(10, 11);
          push r0
          mov r0, 10
          push r0
          ldr r0, fp, -2
          push r0
          bl @set$p1Aii
          add sp, sp, 3
                            
          ldr r0, fp, -2    ;    q->display();
          push r0
          bl @display$p1A
          add sp, sp, 1
                            
          mov r0, 0         ;    return 0;
          mov sp, fp
          pop fp
          pop lr
          ret
                            ; }
malloc:   ld r0, @avail
          add r1, r0, r1
          st r1, @avail
          ret
@avail:   .word @avail + 1
